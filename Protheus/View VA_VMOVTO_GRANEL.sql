SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- Cooperativa Vinicola Nova Alianca Ltda
-- View para buscar movimentacao de notas de produtos a granel.
-- Visa auxiliar na conferencia de guias de livre transito e Sisdeclara.
-- Autor: Robert Koch
-- Data:  03/03/2015
-- Historico de alteracoes:
-- 03/12/2015 - Robert  - Incluido campo F4_PODER3
-- 09/08/2018 - Robert  - Incluido codigo da transportadora junto ao nome.
-- 17/09/2020 - Robert  - Coluna DATA renomeada para DT_MOVTO
-- 28/12/2021 - Claudia - Incluidas colunas de placa e CTE
-- 03/12/2022 - Claudia - Ajustada a busca da placa
-- 19/07/2022 - Robert  - Usar TOP1 na leitura do ZZT para NF de entrada
--

ALTER VIEW [dbo].[VA_VMOVTO_GRANEL] AS

SELECT
	'ENTRADA' AS ENTSAI
   ,D1_FILIAL AS FILIAL
   ,CASE SD1.D1_TIPO
		WHEN 'B' THEN 'REM.BENEF'
		WHEN 'N' THEN 'NORMAL'
		WHEN 'C' THEN 'COMPL.PRECO/FRETE'
		WHEN 'I' THEN 'ICMS'
		WHEN 'P' THEN 'IPI'
		ELSE SD1.D1_TIPO
	END AS TIPO_NF
   ,SD1.D1_TES AS TES
   ,SF4.F4_ESTOQUE AS MOVIM_ESTQ
   ,CASE SF4.F4_PODER3
		WHEN 'R' THEN 'REMESSA'
		WHEN 'D' THEN 'DEVOLUCAO'
		WHEN 'N' THEN 'NAO CONTROLA'
		ELSE SF4.F4_PODER3
	END AS MOVIM_3OS
   ,D1_DOC AS DOC
   ,D1_COD AS PRODUTO
   ,RTRIM(B1_DESC) AS DESCRICAO
   ,D1_QUANT AS QUANT
   ,SD1.D1_UM AS UM
   ,SD1.D1_DTDIGIT AS DT_MOVTO
   ,SD1.D1_FORNECE AS CLI_FORN
   ,SD1.D1_LOJA AS LOJA
   ,CASE
		WHEN SD1.D1_TIPO IN ('B', 'D') THEN RTRIM(SA1.A1_NREDUZ)
		ELSE RTRIM(SA2.A2_NREDUZ)
	END AS NOME
   ,SF1.F1_VAGUIA AS GUIA_TRANSITO
   ,'' AS NUM_LABORAT_GUIA
   ,ISNULL((SELECT
			RTRIM(A2_COD) + '-' + RTRIM(A2_NOME)
		FROM SA2010 SA2_TR
		WHERE SA2_TR.D_E_L_E_T_ = ''
		AND SA2_TR.A2_FILIAL = '  '
		AND SA2_TR.A2_COD + SA2_TR.A2_LOJA = (SELECT TOP 1
				F8_TRANSP + F8_LOJTRAN
			FROM dbo.VA_FFRETES_ENTRADAS(SD1.D1_FILIAL, SD1.D1_FILIAL, SD1.D1_DTDIGIT, SD1.D1_DTDIGIT, SD1.D1_FORNECE, SD1.D1_LOJA, SD1.D1_FORNECE, SD1.D1_LOJA, SD1.D1_COD, SD1.D1_COD, SD1.D1_DOC, SD1.D1_SERIE, SD1.D1_DOC, SD1.D1_SERIE, '', '', 'ZZZZZZ', 'ZZ', '', '', 'ZZZZZZZZZ', 'ZZZ')))
	, '')

	AS NOME_TRANSP
   ,(SELECT TOP 1  -- usa TOP1 por que jah tive casos do pessoal a mesma chave em 2 tickets
			ZZT_PLACA
		FROM ZZT010
		WHERE D_E_L_E_T_ = ''
		AND ZZT_FILIAL = SF1.F1_FILIAL
		AND ZZT_CHVNFE = SF1.F1_CHVNFE
		AND ZZT_ENTSAI = 'E')
	AS PLACA
   ,SF1_CTE.F1_DOC AS CTE
   ,SF1_CTE.F1_SERIE AS SERIE_CTE
   ,SF1_CTE.F1_CHVNFE AS CHAVE_CTE

FROM SD1010 SD1
LEFT JOIN SA1010 SA1
	ON SA1.D_E_L_E_T_ = ''
		AND SA1.A1_FILIAL = '  '
		AND SA1.A1_COD = SD1.D1_FORNECE
		AND SA1.A1_LOJA = SD1.D1_LOJA

LEFT JOIN SA2010 SA2
	ON SA2.D_E_L_E_T_ = ''
		AND SA2.A2_FILIAL = '  '
		AND SA2.A2_COD = SD1.D1_FORNECE
		AND SA2.A2_LOJA = SD1.D1_LOJA

INNER JOIN SF1010 SF1
	ON SF1.D_E_L_E_T_ = ''
		AND SF1.F1_FILIAL = SD1.D1_FILIAL
		AND SF1.F1_DOC = SD1.D1_DOC
		AND SF1.F1_SERIE = SD1.D1_SERIE
		AND SF1.F1_FORNECE = SD1.D1_FORNECE
		AND SF1.F1_LOJA = SD1.D1_LOJA

INNER JOIN SF4010 SF4
	ON SF4.D_E_L_E_T_ = ''
		AND SF4.F4_FILIAL = '  '
		AND SF4.F4_CODIGO = SD1.D1_TES

INNER JOIN SB1010 SB1
	ON SB1.D_E_L_E_T_ = ''
		AND SB1.B1_FILIAL = '  '
		AND SB1.B1_COD = SD1.D1_COD
		AND SB1.B1_GRPEMB = '18'

LEFT JOIN SF8010 SF8
	ON F8_FILIAL = SF1.F1_FILIAL
		AND F8_NFORIG = SF1.F1_DOC
		AND F8_SERORIG = SF1.F1_SERIE
		AND SF8.F8_FORNECE = SF1.F1_FORNECE

LEFT JOIN SF1010 SF1_CTE
	ON SF1_CTE.F1_FILIAL = SF8.F8_FILIAL
		AND SF1_CTE.F1_DOC = SF8.F8_NFDIFRE
		AND SF1_CTE.F1_SERIE = SF8.F8_SEDIFRE
		AND SF1_CTE.F1_FORNECE = SF8.F8_TRANSP

WHERE SD1.D_E_L_E_T_ = ''

UNION ALL

SELECT
	'SAIDA' AS ENTSAI
   ,D2_FILIAL AS FILIAL
   ,CASE SD2.D2_TIPO
		WHEN 'B' THEN 'REM.BENEF'
		WHEN 'N' THEN 'NORMAL'
		WHEN 'C' THEN 'COMPL.PRECO/FRETE'
		WHEN 'I' THEN 'ICMS'
		WHEN 'P' THEN 'IPI'
		ELSE SD2.D2_TIPO
	END AS TIPO_NF
   ,SD2.D2_TES AS TES
   ,SF4.F4_ESTOQUE AS MOVIM_ESTQ
   ,CASE SF4.F4_PODER3
		WHEN 'R' THEN 'REMESSA'
		WHEN 'D' THEN 'DEVOLUCAO'
		WHEN 'N' THEN 'NAO CONTROLA'
		ELSE SF4.F4_PODER3
	END AS MOVIM_3OS
   ,D2_DOC AS DOC
   ,D2_COD AS PRODUTO
   ,RTRIM(B1_DESC) AS DESCRICAO
   ,D2_QUANT AS QUANT
   ,SD2.D2_UM AS UM
   ,SD2.D2_EMISSAO AS DT_MOVTO
   ,SD2.D2_CLIENTE AS CLI_FORN
   ,SD2.D2_LOJA AS LOJA
   ,CASE
		WHEN SD2.D2_TIPO IN ('B', 'D') THEN RTRIM(SA2.A2_NREDUZ)
		ELSE RTRIM(SA1.A1_NREDUZ)
	END AS NOME
   ,SF2.F2_VAGUIA AS GUIA_TRANSITO
   ,ISNULL(SZQ.ZQ_NUMLAB, '') AS NUM_LABORAT_GUIA
   ,ISNULL(RTRIM(SA4.A4_COD) + '-' + RTRIM(SA4.A4_NOME), '') AS NOME_TRANSP
   ,IIF((SELECT
		TOP 1
			ZZT_PLACA
		FROM ZZT010
		WHERE D_E_L_E_T_ = ''
		AND ZZT_FILIAL = SF2.F2_FILIAL
		AND ZZT_CHVNFE = SF2.F2_CHVNFE
		AND ZZT_ENTSAI = 'S')
	<> '', (SELECT
		TOP 1
			ZZT_PLACA
		FROM ZZT010
		WHERE D_E_L_E_T_ = ''
		AND ZZT_FILIAL = SF2.F2_FILIAL
		AND ZZT_CHVNFE = SF2.F2_CHVNFE
		AND ZZT_ENTSAI = 'S')
	, (SELECT
			C5_VEICULO
		FROM SC5010
		WHERE D_E_L_E_T_ = ''
		AND C5_FILIAL = SF2.F2_FILIAL
		AND C5_NOTA = SF2.F2_DOC
		AND C5_SERIE = SF2.F2_SERIE)
	)
	AS PLACA
   ,SF2_CTE.F2_DOC AS CTE
   ,SF2_CTE.F2_SERIE AS SERIE_CTE
   ,SF2_CTE.F2_CHVNFE AS CHAVE_CTE

FROM SD2010 SD2
LEFT JOIN SA1010 SA1
	ON SA1.D_E_L_E_T_ = ''
		AND SA1.A1_FILIAL = '  '
		AND SA1.A1_COD = SD2.D2_CLIENTE
		AND SA1.A1_LOJA = SD2.D2_LOJA

LEFT JOIN SA2010 SA2
	ON SA2.D_E_L_E_T_ = ''
		AND SA2.A2_FILIAL = '  '
		AND SA2.A2_COD = SD2.D2_CLIENTE
		AND SA2.A2_LOJA = SD2.D2_LOJA

INNER JOIN SF4010 SF4
	ON SF4.D_E_L_E_T_ = ''
		AND SF4.F4_FILIAL = '  '
		AND SF4.F4_CODIGO = SD2.D2_TES

INNER JOIN SB1010 SB1
	ON SB1.D_E_L_E_T_ = ''
		AND SB1.B1_FILIAL = '  '
		AND SB1.B1_COD = SD2.D2_COD
		AND SB1.B1_GRPEMB = '18'

INNER JOIN SF2010 SF2
	ON SF2.D_E_L_E_T_ = ''
		AND SF2.F2_FILIAL = SD2.D2_FILIAL
		AND SF2.F2_DOC = SD2.D2_DOC

LEFT JOIN SZQ010 SZQ
	ON SZQ.D_E_L_E_T_ = ''
		AND SZQ.ZQ_FILIAL = SF2.F2_FILIAL
		AND SZQ.ZQ_NUMERO = SF2.F2_VAGUIA

LEFT JOIN SA4010 SA4
	ON SA4.D_E_L_E_T_ = ''
		AND SA4.A4_FILIAL = '  '
		AND SA4.A4_COD = SF2.F2_TRANSP

LEFT JOIN SF8010 SF8
	ON F8_FILIAL = SF2.F2_FILIAL
		AND F8_NFORIG = SF2.F2_DOC
		AND F8_SERORIG = SF2.F2_SERIE
		AND SF8.F8_FORNECE = SF2.F2_CLIENTE

LEFT JOIN SF2010 SF2_CTE
	ON SF2_CTE.F2_FILIAL = SF8.F8_FILIAL
		AND SF2_CTE.F2_DOC = SF8.F8_NFDIFRE
		AND SF2_CTE.F2_SERIE = SF8.F8_SEDIFRE
		AND SF2_CTE.F2_CLIENTE = SF8.F8_TRANSP

WHERE SD2.D_E_L_E_T_ = ''

GO
