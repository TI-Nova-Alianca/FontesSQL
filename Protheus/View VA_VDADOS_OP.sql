SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


ALTER VIEW [dbo].[VA_VDADOS_OP] AS (
	-- Cooperativa Vinicola Nova Alianca Ltda
	-- View para buscar dados de ordens de producao.
	-- Autor: Robert Koch
	-- Data:  04/05/2012
	-- Historico de alteracoes:
	-- 25/05/2012 - Robert - Incluida coluna com a litragem movimentada.
	-- 11/08/2015 - Robert - Passa a tratar campo D3_VAOPESP
	-- 06/10/2016 - Robert - Nao considerava os campos G1_TRT e D4_TRT nos joins. Passou a duplicar resultados quando adotamos o controle de revisoes de estruturas.
	-- 02/09/2017 - Robert - Removida coluna TIPO_OP (campo C2_VATIPO vai ser removido da base de dados, junto com os triggers que o alimentam).
	-- 20/03/2018 - Robert - Removida leitura do SD4 (empenhos) pois gerava produto cartesiano.
	-- 18/08/2021 - Robert - Incluida coluna TIPO_PROD_FINAL (GLPI 10647)
	--
	  
	-- LEITURA DOS MOVIMENTOS DE PRODUCAO DA OP
	SELECT OP.FILIAL,
			OP.OP,
			--OP.TIPO_OP,
			OP.FINALIDADE_OP,
			PRODUCOES.D3_EMISSAO AS DATA,
			'P' AS TIPO_MOVTO,
			OP.PROD_FINAL,
			OP.B1_DESC AS DESC_PROD_FINAL,
			OP.EMISSAO, OP.ENCERRAMENTO,
			OP.PROD_FINAL AS CODIGO,
			OP.B1_DESC AS DESCRICAO,
			PRODUCOES.D3_QUANT AS QUANT_REAL,
			PRODUCOES.D3_UM AS UN_MEDIDA,
			PRODUCOES.D3_LOCAL AS LOCAL,
			PRODUCOES.D3_CUSTO1 AS CUSTO,
			0 AS PREV_ESTRUT,
			--0 AS QUANT_EMPENHO,
			PRODUCOES.D3_DOC AS DOCTO,
			PRODUCOES.D3_USUARIO AS USUARIO,
			'' AS NF_BENEF,
			'' AS SERIE_BENEF,
			'' AS FORNEC_BENEF,
			'' AS LOJA_BENEF,
			'' AS NOME_BENEF,
			PRODUCOES.D3_GRUPO AS GRUPO,
			PRODUCOES.D3_TIPO AS TIPO_PRODUTO,
			PRODUCOES.D3_QUANT * OP.B1_LITROS AS LITROS,
			OP.TIPO_PROD_FINAL
	FROM   (
				-- 'DERIVED QUERY' PARA BUSCAR OS DADOS GERAIS DA OP. MANTER IGUAL A DA PROXIMA QUERY.
				SELECT SC2.C2_FILIAL AS FILIAL,
					SC2.C2_NUM + SC2.C2_ITEM + SC2.C2_SEQUEN + SC2.C2_ITEMGRD AS OP,
					SC2.C2_PRODUTO AS PROD_FINAL,
					SC2.C2_QUANT AS QUANT_PREV,
					--SC2.C2_VATIPO AS TIPO_OP,
					SC2.C2_EMISSAO AS EMISSAO,
					SC2.C2_DATRF AS ENCERRAMENTO,
					SB1.B1_DESC,
					SB1.B1_QB,
					SB1.B1_LITROS,
					SC2.C2_VAOPESP AS FINALIDADE_OP,
					SB1.B1_TIPO AS TIPO_PROD_FINAL
				FROM   SC2010 SC2,
					SB1010 SB1
				WHERE  SB1.D_E_L_E_T_ = ''
					AND SB1.B1_FILIAL = '  '
					AND SB1.B1_COD = SC2.C2_PRODUTO
					AND SC2.D_E_L_E_T_ = ''
					and SC2.C2_FILIAL not like 'S%'
			) AS OP
			LEFT JOIN SD3010 PRODUCOES
				ON  (
						PRODUCOES.D_E_L_E_T_ = ''
						AND PRODUCOES.D3_FILIAL = OP.FILIAL
						AND PRODUCOES.D3_ESTORNO != 'S'
						AND PRODUCOES.D3_CF LIKE 'PR%'
						AND PRODUCOES.D3_OP = OP.OP
					)
	  
	UNION ALL
	  
	-- LEITURA DOS MOVIMENTOS DE CONSUMO / DEVOLUCAO DA OP
	SELECT OP.FILIAL,
			OP.OP,
			OP.FINALIDADE_OP,
			CONSUMOS.D3_EMISSAO AS DATA,
			CASE 
				WHEN CONSUMOS.D3_CF LIKE 'RE%' THEN 'C'
				ELSE CASE 
						WHEN CONSUMOS.D3_CF LIKE 'DE%' THEN 'D'
						ELSE ''
					END
			END AS TIPO_MOVTO,
			OP.PROD_FINAL,
			OP.B1_DESC AS DESC_PROD_FINAL,
			OP.EMISSAO, OP.ENCERRAMENTO,
			CONSUMOS.D3_COD AS CODIGO,
			PROD_CONSUMIDO.B1_DESC AS DESCRICAO,
			CONSUMOS.D3_QUANT AS QUANT_REAL,
			CONSUMOS.D3_UM AS UN_MEDIDA,
			CONSUMOS.D3_LOCAL AS LOCAL,
			CONSUMOS.D3_CUSTO1 AS CUSTO,
			-- BUSCA QUANTIDADE PREVISTA PELA ESTRUTURA
			ISNULL(
				CONSUMOS.D3_QUANT * SG1.G1_QUANT /
				CASE OP.B1_QB
					WHEN 0 THEN 1
					ELSE OP.B1_QB
				END
				* CASE SG1.G1_PERDA
					WHEN 0 THEN 1
					ELSE SG1.G1_PERDA / 100
				END,
				0
			) AS PREV_ESTRUT,
			CONSUMOS.D3_DOC AS DOCTO,
			CONSUMOS.D3_USUARIO AS USUARIO,
			ISNULL(NF_BENEF.D1_DOC, '') AS NF_BENEF,
			ISNULL(NF_BENEF.D1_SERIE, '') AS SERIE_BENEF,
			ISNULL(NF_BENEF.D1_FORNECE, '') AS FORNEC_BENEF,
			ISNULL(NF_BENEF.D1_LOJA, '') AS LOJA_BENEF,
			ISNULL(TERCEIRO.A2_NOME, '') AS NOME_BENEF,
			CONSUMOS.D3_GRUPO AS GRUPO,
			CONSUMOS.D3_TIPO AS TIPO_PRODUTO,
			CONSUMOS.D3_QUANT * PROD_CONSUMIDO.B1_LITROS AS LITROS,
			OP.TIPO_PROD_FINAL
	FROM   (
				-- 'DERIVED QUERY' PARA BUSCAR OS DADOS GERAIS DA OP. MANTER IGUAL A DA QUERY ANTERIOR.
				SELECT SC2.C2_FILIAL AS FILIAL,
					SC2.C2_NUM + SC2.C2_ITEM + SC2.C2_SEQUEN + SC2.C2_ITEMGRD AS OP,
					SC2.C2_PRODUTO AS PROD_FINAL,
					SC2.C2_QUANT AS QUANT_PREV,
					--SC2.C2_VATIPO AS TIPO_OP,
					SC2.C2_EMISSAO AS EMISSAO,
					SC2.C2_DATRF AS ENCERRAMENTO,
					SB1.B1_DESC,
					SB1.B1_QB,
					SB1.B1_LITROS,
					SC2.C2_VAOPESP AS FINALIDADE_OP,
					SB1.B1_TIPO AS TIPO_PROD_FINAL
				FROM   SC2010 SC2,
					SB1010 SB1
				WHERE  SB1.D_E_L_E_T_ = ''
					AND SB1.B1_FILIAL = '  '
					AND SB1.B1_COD = SC2.C2_PRODUTO
					AND SC2.D_E_L_E_T_ = ''
					and SC2.C2_FILIAL not like 'S%'
			) AS OP
			LEFT JOIN SD3010 CONSUMOS
			JOIN SB1010 PROD_CONSUMIDO
				ON  (
						PROD_CONSUMIDO.D_E_L_E_T_ = ''
						AND PROD_CONSUMIDO.B1_FILIAL = '  '
						AND PROD_CONSUMIDO.B1_COD = CONSUMOS.D3_COD
					)
				ON  (
						CONSUMOS.D_E_L_E_T_ = ''
						AND CONSUMOS.D3_FILIAL = OP.FILIAL
						AND CONSUMOS.D3_ESTORNO != 'S'
						AND CONSUMOS.D3_CF NOT LIKE 'PR%'
						AND CONSUMOS.D3_OP = OP.OP
					)
			LEFT JOIN SG1010 SG1
				ON  (
						SG1.D_E_L_E_T_ = ''
						AND SG1.G1_FILIAL = '  '
						AND G1_COD = OP.PROD_FINAL
						AND SG1.G1_COMP = CONSUMOS.D3_COD
						AND SG1.G1_TRT = CONSUMOS.D3_TRT
					)
	                  
			-- BUSCA DADOS DA NF DE BENEFICIAMENTO EXTERNO, QUANDO EXISTIR
			LEFT JOIN SD1010 NF_BENEF
			JOIN SA2010 TERCEIRO
				ON  (
						TERCEIRO.D_E_L_E_T_ = ''
						AND TERCEIRO.A2_FILIAL = '  '
						AND TERCEIRO.A2_COD = NF_BENEF.D1_FORNECE
						AND TERCEIRO.A2_LOJA = NF_BENEF.D1_LOJA
					)
				ON  (
						NF_BENEF.D_E_L_E_T_ = ''
						AND NF_BENEF.D1_FILIAL = CONSUMOS.D3_FILIAL
						AND NF_BENEF.D1_NUMSEQ = CONSUMOS.D3_NUMSEQ
						AND NF_BENEF.D1_DOC = CONSUMOS.D3_DOC
					)
	)
GO
