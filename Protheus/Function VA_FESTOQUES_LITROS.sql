SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- Descricao: Retorna saldos em estoques, convertidos para litros, na data informada.
--            Criado com base na consulta de mesmo nome do SQLServer Reporting Services.
-- Autor....: Daiana Ribas
-- Data.....: 22/08/2022
--
-- Historico de alteracoes:
-- 21/10/2022 - Robert - Incluídas colunas de custo médio dos saldos.
--                     - Devido lentidao leitura custo, passa a gerar em varias
--                       etapas (CTEs) para melhorar um pouco a performance.
-- 08/11/2022 - Robert - Passa a considerar tambem itens tipo PP (produto em processo)
--

ALTER FUNCTION [dbo].[VA_FESTOQUES_LITROS]
-- PARAMETROS DE CHAMADA
(	
	@DATAREF AS VARCHAR(8),
	@FILIALINI AS VARCHAR(2),
	@FILIALFIM AS VARCHAR(2)
)
RETURNS TABLE
AS

RETURN

-- GERA UMA CTE (COMMON TABLE EXPRESSION) PARA CALCULAR O SALDO EM LITROS DE
-- CADA PRODUTO DO ESTOQUE.
WITH C AS (
	SELECT B2_FILIAL AS FILIAL
		, RTRIM (B1_COD) AS PRODUTO
		, RTRIM (B1_DESC) AS DESCRI_PRODUTO
		, B1_GRPEMB
		, SB1.B1_LITROS
		, SB2.B2_LOCAL
		, dbo.VA_SALDOESTQ (SB2.B2_FILIAL, SB2.B2_COD, SB2.B2_LOCAL, @DATAREF) AS SALDO_QT
		, SB1.B1_UM
	FROM SB1010 SB1,
		SB2010 SB2
	WHERE SB2.D_E_L_E_T_ = ''
	AND B2_FILIAL BETWEEN @FILIALINI AND @FILIALFIM
	AND B2_FILIAL != '02'  -- BAIXADA, MAS VOLTA E MEIA SURGE SALDO FANTASMA.
	AND SB2.B2_COD = SB1.B1_COD
	AND SB1.D_E_L_E_T_ = ''
	AND SB1.B1_FILIAL = '  '
	AND SB1.B1_TIPO IN ('PA', 'PI', 'VD', 'PP')--Somente os litros - Fixos a pedido da Sara - DayRibas

	-- HABILITAR ESTA CLAUSULA SE QUISER GERAR VALORES IGUAIS AO RELATORIO ANTIGO, QUE CONSIDERAVA OS ALMOXARIFADOS DA TABELA SX5 (MIGRADA PARA TABELA NNR)
	-- AND SB2.B2_LOCAL IN ('01', '02', '03', '07', '08', '09', '10', '11', '13', '20', '21', '25', '50', '60','66', '70', '71', '72', '73', '90', '91', '92', '99')
	-- and exists (SELECT * FROM SX5010 WHERE D_E_L_E_T_ = '' AND X5_FILIAL = '  ' AND X5_TABELA = 'AL' AND X5_CHAVE = SB2.B2_LOCAL)
	-- O RELATORIO ANTIGO ESTAH, PORTANTO, ERRADO, POIS NAO CONSIDERA TODOS OS ALMOXARIFADOS EXISTENTES.
)

-- GERA NOVA CTE PARA ADICIONAR COLUNA COM O CUSTO. NAO FIZ NA PRIMEIRA CTE POR
-- QUE AINDA NAO TINHA O SALDO (QUANTIDADE) NA DATA DE REFERENCIA.
, D AS (
	SELECT C.*
		-- O CUSTO EH BUSCADO DO SB9 (FECHAMENTO) MAIS RECENTE.
		, ISNULL ((SELECT TOP 1 B9_VINI1 / B9_QINI
			FROM SB9010 SB9
			WHERE SB9.D_E_L_E_T_ = ''
			AND SB9.B9_FILIAL = C.FILIAL
			AND SB9.B9_COD = C.PRODUTO
			AND SB9.B9_LOCAL = C.B2_LOCAL
			AND SB9.B9_DATA <= @DATAREF
			AND B9_QINI > 0
			AND B9_VINI1 > 0
			ORDER BY B9_DATA DESC
		), 0) AS CUSTO_UNITARIO
	FROM C
	WHERE SALDO_QT != 0
)

, E AS (
	SELECT D.*
		, CASE D.B1_GRPEMB
			WHEN '18' THEN D.SALDO_QT * D.B1_LITROS
			ELSE 0
		END AS ESTQ_LITROS_GRANEL
		, CASE D.B1_GRPEMB
			WHEN '18' THEN 0
			ELSE D.SALDO_QT * D.B1_LITROS
		END AS ESTQ_LITROS_ENVASADO
		, CASE D.B1_GRPEMB
			WHEN '18' THEN D.SALDO_QT * D.CUSTO_UNITARIO
			ELSE 0
		END AS CUSTO_MEDIO_GRANEL
		, CASE D.B1_GRPEMB
			WHEN '18' THEN 0
			ELSE D.SALDO_QT * D.CUSTO_UNITARIO
		END AS CUSTO_MEDIO_ENVASADO
	FROM D
)

-- ACUMULA POR FILIAL (NO MOMENTO NAO HA INTERESSE EM LISTA SALDO POR ALMOX)
SELECT 'GERAL' AS GERAL
	, E.FILIAL
	, RTRIM (SM0.M0_FILIAL) AS DESCRI_FILIAL
	, E.PRODUTO
	, E.DESCRI_PRODUTO
	, SUM (E.ESTQ_LITROS_GRANEL)   AS ESTQ_LITROS_GRANEL
	, SUM (E.ESTQ_LITROS_ENVASADO) AS ESTQ_LITROS_ENVASADO
	, SUM (E.ESTQ_LITROS_GRANEL + E.ESTQ_LITROS_ENVASADO) as ESTQ_LITROS_TOTAL 
	, ROUND (SUM (E.CUSTO_MEDIO_GRANEL), 4)   AS CUSTO_MEDIO_GRANEL
	, ROUND (SUM (E.CUSTO_MEDIO_ENVASADO), 4) AS CUSTO_MEDIO_ENVASADO
	, ROUND (SUM (E.CUSTO_MEDIO_GRANEL + E.CUSTO_MEDIO_ENVASADO), 4) as CUSTO_MEDIO_TOTAL
	, SUM (E.SALDO_QT) AS ESTQ_UM_ORIG
	, E.B1_UM AS UM_ORIG
FROM E
	LEFT JOIN SYS_COMPANY SM0
		ON (
				SM0.D_E_L_E_T_ = ''
				AND SM0.M0_CODIGO = '01'
				AND SM0.M0_CODFIL = E.FILIAL
			)
GROUP BY E.FILIAL
	, M0_FILIAL
	, E.PRODUTO
	, E.DESCRI_PRODUTO
	, E.B1_UM
