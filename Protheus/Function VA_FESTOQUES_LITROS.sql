SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- Descricao: Retorna saldos em estoques, convertidos para litros, na data informada.
--            Criado com base na consulta de mesmo nome do SQLServer Reporting Services.
-- Autor....: Daiana Ribas
-- Data.....: 22/08/2022
--
-- Historico de alteracoes:
--

ALTER FUNCTION [dbo].[VA_FESTOQUES_LITROS]
-- PARAMETROS DE CHAMADA
(	
	@DATAREF AS VARCHAR(8),
	@FILIALINI AS VARCHAR(2),
	@FILIALFIM AS VARCHAR(2)
)
RETURNS TABLE
AS

RETURN

-- GERA UMA CTE (COMMON TABLE EXPRESSION) PARA CALCULAR O SALDO EM LITROS DE
-- CADA PRODUTO DO ESTOQUE.
WITH C AS (
	SELECT
		 B2_FILIAL AS FILIAL
		,RTRIM (B1_COD) AS PRODUTO
		,RTRIM(B1_DESC) AS DESCRI_PRODUTO
		,CASE SB1.B1_GRPEMB
			WHEN '18' THEN dbo.VA_SALDOESTQ (SB2.B2_FILIAL, SB2.B2_COD, SB2.B2_LOCAL, @DATAREF) * SB1.B1_LITROS
			ELSE 0
		END AS ESTQ_LITROS_GRANEL
		,CASE SB1.B1_GRPEMB
			WHEN '18' THEN 0
			ELSE dbo.VA_SALDOESTQ (SB2.B2_FILIAL, SB2.B2_COD, SB2.B2_LOCAL, @DATAREF) * SB1.B1_LITROS
		END AS ESTQ_LITROS_ENVASADO
	FROM SB1010 SB1,
		SB2010 SB2
	WHERE SB2.D_E_L_E_T_ = ''
	AND B2_FILIAL BETWEEN @FILIALINI AND @FILIALFIM
	AND B2_FILIAL != '02'  -- BAIXADA, MAS VOLTA E MEIA SURGE SALDO FANTASMA.
	AND SB2.B2_COD = SB1.B1_COD
	AND SB1.D_E_L_E_T_ = ''
	AND SB1.B1_FILIAL = '  '
	AND SB1.B1_TIPO IN ('PA', 'PI', 'VD')--Somente os litros - Fixos a pedido da Sara - DayRibas


	-- HABILITAR ESTA CLAUSULA SE QUISER GERAR
	-- VALORES IGUAIS AO RELATORIO ANTIGO, QUE CONSIDERAVA OS
	-- ALMOXARIFADOS DA TABELA SX5 (MIGRADA PARA TABELA NNR)
	--AND SB2.B2_LOCAL IN ('01', '02', '03', '07', '08', '09', '10', '11', '13', '20', '21', '25', '50', '60','66', '70', '71', '72', '73', '90', '91', '92', '99')
	-- O RELATORIO ANTIGO ESTAH, PORTANTO, ERRADO, POIS NAO
	-- CONSIDERA TODOS OS ALMOXARIFADOS EXISTENTES.


)

-- JUNTA TABELA SYS_COMPANY (ANTIGA VA_SM0) PARA TER OS NOMES
-- DAS FILIAIS. CONFERIR SE PRECISA MESMO, DO CONTRARIO, ELIMINA-LA.
SELECT --'GERAL' AS GERAL,
	C.FILIAL
	,RTRIM(SM0.M0_FILIAL) AS DESCRI_FILIAL
	,C.PRODUTO
	,C.DESCRI_PRODUTO
	,SUM (C.ESTQ_LITROS_GRANEL) AS ESTQ_LITROS_GRANEL
	,SUM (C.ESTQ_LITROS_ENVASADO) AS ESTQ_LITROS_ENVASADO
	,SUM (C.ESTQ_LITROS_GRANEL + C.ESTQ_LITROS_ENVASADO) as ESTQ_LITROS_TOTAL 
FROM C
	LEFT JOIN SYS_COMPANY SM0
		ON (
				SM0.D_E_L_E_T_ = ''
				AND SM0.M0_CODIGO = '01'
				AND SM0.M0_CODFIL = C.FILIAL
			)
WHERE C.ESTQ_LITROS_GRANEL != 0 OR C.ESTQ_LITROS_ENVASADO != 0
GROUP BY C.FILIAL
	,M0_FILIAL
	,C.PRODUTO
	,C.DESCRI_PRODUTO--, C.ESTQ_LITROS_GRANEL, C.ESTQ_LITROS_ENVASADO
GO
