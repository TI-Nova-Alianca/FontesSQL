USE [protheus]
GO

/****** Object:  View [dbo].[VA_VFAT]    Script Date: 20/04/2022 15:53:25 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



ALTER VIEW [dbo].[VA_VFAT]
AS
-- Cooperativa Vinicola Nova Alianca Ltda
-- View para buscar dados gerais de faturamento.
-- Criada com base na tabela fisica VA_FAT2, que pretendemos eliminar.
-- Autor: Robert Koch
-- Data:  30/09/2015
-- Historico de alteracoes:
-- 07/11/2015 - Robert  - Finalizado calculo de fretes e despesas financeiras.
-- 27/06/2016 - Robert  - Desconsidera D1_TIPO, pois o que vale eh o campo F4_MARGEM.
-- 06/11/2017 - Robert  - Passa a considerar D2_RAPEL e nao mais F2_VAPERAP a partir de out/2017.
-- 25/02/2019 - Catia   - Vendedor nas notas de devolucao buscar pelo financeiro no SE1
-- 22/10/2019 - Robert  - Gera FRETEPREV zerado para devolucoes (vamos assumir que nao existe, pois vendemos sem intencao de ter o produto devolvido depois.
-- 05/11/2019 - Claudia - Realizada a troca da multiplicação de qnt*vlr unitario para valor total para notas de entrada, devido a problemas nas devoluções.
-- 24/02/2020 - Claudia - Incluido case na verificação do campo RAPELPREV, para não dar erro de divisão por zero
-- 30/03/2020 - Robert  - Aumentado tamanho dos campos D2_ITEM e D1_ITEMORI, D2_ITEMPV, C6_ITEM (GLPI 7740)
-- 02/04/2020 - Claudia - Voltada as alterações D2_ITEM e D1_ITEMORI, D2_ITEMPV, C6_ITEM (GLPI 7741)

SELECT
	'01' AS EMPRESA
   ,'SD2' AS ORIGEM
   ,SD2.D2_FILIAL AS FILIAL
   ,SD2.D2_TES AS TES
   ,SD2.D2_CLIENTE AS CLIENTE
   ,SD2.D2_LOJA AS LOJA
   ,SD2.D2_GRUPO AS GRUPOPROD
   ,SD2.D2_COD AS PRODUTO
   ,SD2.D2_TP AS TIPOPROD
   ,' ' AS TIPONFENTR
   ,SD2.D2_TIPO AS TIPONFSAID
   ,SD2.D2_QUANT AS QUANTIDADE
   ,CASE SD2.D2_TP
		WHEN 'PA' THEN SD2.D2_QUANT * SB1.B1_LITROS
		WHEN 'PI' THEN SD2.D2_QUANT * SB1.B1_LITROS
		WHEN 'VD' THEN SD2.D2_QUANT * SB1.B1_LITROS
		ELSE 0
	END AS QTLITROS
   ,SD2.D2_DOC AS DOC
   ,SD2.D2_SERIE AS SERIE
   ,SD2.D2_EMISSAO AS EMISSAO
   ,SF2.F2_VEND1 AS VEND1
   ,SF2.F2_VEND2 AS VEND2
   ,SD2.D2_PEDIDO AS PEDVENDA
   ,SD2.D2_ITEMPV AS ITEMPDVEND
   ,SD2.D2_PVCOND AS PVCOND
   ,SD2.D2_SEGURO AS SEGURO
   ,SD2.D2_DESPESA AS DESPESA
   ,SD2.D2_PRCVEN * SD2.D2_QUANT AS TOTAL
   ,SD2.D2_VALIPI AS VALIPI
   ,SD2.D2_COMIS1 AS COMIS1
   ,SD2.D2_COMIS2 AS COMIS2
   ,SD2.D2_COMIS3 AS COMIS3
   ,SD2.D2_COMIS4 AS COMIS4
   ,SD2.D2_COMIS5 AS COMIS5
   ,SD2.D2_VRAPEL AS RAPELPREV
   ,CASE SA1.A1_VABARAP
		WHEN '0' THEN 'NAO'
		WHEN '1' THEN 'TOTAL NOTA'
		WHEN '2' THEN 'TOTAL MERCADORIA'
		WHEN '3' THEN 'TOTAL NF - ST'
	END AS BASERAPEL
   ,SD2.D2_VALICM AS VALICM
   ,SD2.D2_VALIMP6 AS VALPIS
   ,SD2.D2_VALIMP5 AS VALCOFINS
   ,SD2.D2_CUSTO1 AS CUSTOMEDIO
   ,dbo.VA_FQtCx(SD2.D2_COD, SD2.D2_QUANT) AS QTCAIXAS
   ,SF4.F4_DUPLIC AS F4_DUPLIC
   ,SF4.F4_ESTOQUE AS F4_ESTOQUE
   ,SF4.F4_MARGEM AS F4_MARGEM
   ,SD2.D2_EST AS EST
   ,'' AS PRODPAI
   ,'         ' AS NFORI
   ,'   ' AS SERIORI
   ,'  ' AS ITEMORI
   ,SD2.D2_ITEM + '  ' AS ITEMNOTA
 --  ,SD2.D2_ITEM AS ITEMNOTA
   ,SB1.B1_P_BRT * SD2.D2_QUANT AS PESOBRUTO
   ,SD2.D2_UM AS UMPROD
   ,'' AS UMPRODPAI
   ,SD2.D2_ICMSRET AS ICMSRET
   ,'  ' AS MOTDEV
   ,SD2.D2_DESCON AS DESCONTO
   ,SD2.D2_PRUNIT AS PRUNIT
   ,D2_CF AS CFOP
   ,F2_TRANSP AS TRANSP
   ,D2_VACUSTD AS CUSTOREPOS
   ,'' AS DTGERACAO
   ,D2_TOTAL + D2_VALIPI + D2_SEGURO + D2_DESPESA + D2_PVCOND AS VALBRUT
   ,CASE
		WHEN D2_EMISSAO < '20171001' THEN ISNULL((D2_TOTAL + D2_PVCOND - D2_VACUSTD * D2_QUANT - (D2_TOTAL + D2_PVCOND) * D2_COMIS1 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS2 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS3 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS4 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS5 / 100 - ISNULL((SD2.D2_TOTAL + SD2.D2_VALIPI + SD2.D2_ICMSRET) * SF2.F2_VAPERAP / 100, 0) - D2_FRETCIF - D2_VALICM - D2_VALIMP6 - D2_VALIMP5), 0)
		ELSE ISNULL((D2_TOTAL + D2_PVCOND - D2_VACUSTD * D2_QUANT - (D2_TOTAL + D2_PVCOND) * D2_COMIS1 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS2 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS3 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS4 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS5 / 100 - ISNULL((SD2.D2_TOTAL + SD2.D2_VALIPI + SD2.D2_ICMSRET) * SD2.D2_RAPEL / 100, 0) - D2_FRETCIF - D2_VALICM - D2_VALIMP6 - D2_VALIMP5), 0)
	END
	AS MARGEMCONT
   ,

	-- FRETES
	ISNULL(SC5.C5_TPFRETE, ' ') AS TIPOFRETE
   ,ISNULL(CASE SF2.F2_PBRUTO
		WHEN 0 THEN 0
		ELSE ROUND(SC5.C5_MVFRE * (SB1.B1_P_BRT * SD2.D2_QUANT) / SF2.F2_PBRUTO, 2)
	END, 0) AS FRETEPREV
   ,SD2.D2_FRETCIF AS VALORFRETE
   ,ISNULL(FRETES.REENTR, 0) AS FRETEREENT
   ,ISNULL(FRETES.REDESP, 0) AS FRETEREDSP
   ,ISNULL(FRETES.PALETIZ, 0) AS FRETEPALET
   ,

	-- DESCONTOS FINANCEIROS
	ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFRAPEL, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFRAPEL
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFENCART, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFENCART
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFFEIRAS, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFFEIRAS
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFFRETES, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFFRETES
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFDESCONT, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFDESCONT
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFDEVOL, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFDEVOL
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFCAMPANH, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFCAMPANH
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFABLOJA, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFABLOJA
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFCONTRAT, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFCONTRAT
   ,ISNULL(ROUND((SD2.D2_TOTAL) * ISNULL(DESCFIN.DFOUTROS, 0) /
	CASE SF2.F2_VALMERC
		WHEN 0 THEN 1
		ELSE SF2.F2_VALMERC
	END, 2), 0) AS DFOUTROS
   ,ISNULL(SA1.A1_SATIV1, '') AS ATIVIDADE
   ,ISNULL((SELECT
			DATEDIFF(DAY, CAST(MIN(E1_EMISSAO) AS SMALLDATETIME), CAST(MIN(E1_VENCREA) AS SMALLDATETIME)) + DATEDIFF(DAY, CAST(MIN(E1_VENCREA) AS SMALLDATETIME), CAST(MAX(E1_VENCREA) AS SMALLDATETIME)) / 2
		FROM SE1010 SE1
		WHERE SE1.E1_FILIAL = SD2.D2_FILIAL
		AND SE1.E1_PREFIXO = SD2.D2_SERIE
		AND SE1.E1_NUM = SD2.D2_DOC
		AND SE1.D_E_L_E_T_ = '')
	, 0)
	AS PRAZOMEDIO
   ,SB1.B1_VAMARCM AS MARCA
   ,SB1.B1_GRPEMB AS GRPEMB
   ,SB1.B1_CODLIN AS CODLINHA
   ,ISNULL(SX5SAtiv.X5_DESCRI, '') AS DESCRIATIV
   ,ISNULL(SA1.A1_VACANAL, '') AS CANAL
   ,ISNULL(ZX5Canal.ZX5_18DESC, '') AS DESCRICANAL
   ,D2_VALFRE
FROM SD2010 AS SD2
INNER JOIN SB1010 AS SB1
	ON (SB1.B1_COD = SD2.D2_COD
			AND SB1.D_E_L_E_T_ <> '*'
			AND SB1.B1_FILIAL = '  ')
LEFT JOIN SA1010 AS SA1
LEFT JOIN SX5010 AS SX5SAtiv
	ON (SX5SAtiv.D_E_L_E_T_ = ''
			AND SX5SAtiv.X5_FILIAL = '  '
			AND SX5SAtiv.X5_TABELA = 'T3'
			AND SX5SAtiv.X5_CHAVE = SA1.A1_SATIV1)
LEFT JOIN ZX5010 AS ZX5Canal
	ON (ZX5Canal.D_E_L_E_T_ = ''
			AND ZX5Canal.ZX5_FILIAL = '  '
			AND ZX5Canal.ZX5_TABELA = '18'
			AND ZX5Canal.ZX5_18COD = SA1.A1_VACANAL)
	ON (SA1.D_E_L_E_T_ = ''
			AND SA1.A1_FILIAL = '  '
			AND SA1.A1_COD = SD2.D2_CLIENTE
			AND SA1.A1_LOJA = SD2.D2_LOJA)
INNER JOIN SF4010 AS SF4
	ON (SD2.D2_TES = SF4.F4_CODIGO
			AND SF4.D_E_L_E_T_ <> '*'
			AND SF4.F4_FILIAL = '  ')
INNER JOIN SF2010 AS SF2
	ON (SD2.D2_DOC = SF2.F2_DOC
			AND SD2.D2_SERIE = SF2.F2_SERIE
			AND SF2.D_E_L_E_T_ <> '*'
			AND SD2.D2_FILIAL = SF2.F2_FILIAL)
LEFT JOIN SC5010 AS SC5
	ON (SC5.C5_NUM = SD2.D2_PEDIDO
			AND SC5.D_E_L_E_T_ <> '*'
			AND SC5.C5_FILIAL = SD2.D2_FILIAL)
LEFT JOIN (SELECT
		SZH.ZH_FILIAL
	   ,SZH.ZH_NFSAIDA
	   ,SZH.ZH_SERNFS
	   ,SZH.ZH_ITNFS
	   ,SUM(CASE
			WHEN SZH.ZH_TPDESP = '2' THEN SZH.ZH_RATEIO
			ELSE 0
		END) AS REENTR
	   ,SUM(CASE
			WHEN SZH.ZH_TPDESP = '3' THEN SZH.ZH_RATEIO
			ELSE 0
		END) AS REDESP
	   ,SUM(CASE
			WHEN SZH.ZH_TPDESP = '4' THEN SZH.ZH_RATEIO
			ELSE 0
		END) AS PALETIZ
	FROM SZH010 AS SZH
	WHERE SZH.D_E_L_E_T_ != '*'
	GROUP BY SZH.ZH_FILIAL
			,SZH.ZH_NFSAIDA
			,SZH.ZH_SERNFS
			,SZH.ZH_ITNFS) AS FRETES
	ON (ZH_FILIAL = SD2.D2_FILIAL
			AND ZH_NFSAIDA = SD2.D2_DOC
			AND ZH_SERNFS = SD2.D2_SERIE
			AND ZH_ITNFS = SD2.D2_ITEM)

-- TOTAIS POR TIPO DE DESCONTO CONCEDIDO NAS BAIXAS DOS TITULOS NO FINANCEIRO.
LEFT JOIN (SELECT
		SE5.E5_FILIAL
	   ,SE5.E5_PREFIXO
	   ,SE5.E5_NUMERO
	   ,SE5.E5_CLIENTE
	   ,SE5.E5_LOJA
	   ,SUM(DISTINCT SE5.E5_VARAPEL) AS DFRAPEL
	   ,SUM(DISTINCT SE5.E5_VAENCAR) AS DFENCART
	   ,SUM(DISTINCT SE5.E5_VAFEIRA) AS DFFEIRAS
	   ,SUM(DISTINCT SE5.E5_VADFRET) AS DFFRETES
	   ,SUM(DISTINCT SE5.E5_VADDESC) AS DFDESCONT
	   ,SUM(DISTINCT SE5.E5_VADDEVO) AS DFDEVOL
	   ,SUM(DISTINCT SE5.E5_VADCMPV) AS DFCAMPANH
	   ,SUM(DISTINCT SE5.E5_VADAREI) AS DFABLOJA
	   ,SUM(DISTINCT SE5.E5_VADMULC) AS DFCONTRAT
	   ,SUM(DISTINCT SE5.E5_VADOUTR) AS DFOUTROS
	FROM SE5010 SE5
	WHERE SE5.D_E_L_E_T_ = ''
	AND SE5.E5_RECPAG = 'R'
	AND SE5.E5_SITUACA != 'C'
	AND SE5.E5_TIPODOC NOT IN ('TR', 'EC', 'ES', 'TE', 'CP', 'JR', 'DC', 'MT', 'V2', 'J2')
	GROUP BY SE5.E5_FILIAL
			,SE5.E5_PREFIXO
			,SE5.E5_NUMERO
			,SE5.E5_CLIENTE
			,SE5.E5_LOJA) AS DESCFIN
	ON (DESCFIN.E5_FILIAL = SD2.D2_FILIAL
			AND DESCFIN.E5_PREFIXO = SD2.D2_SERIE
			AND DESCFIN.E5_NUMERO = SD2.D2_DOC
			AND DESCFIN.E5_CLIENTE = SD2.D2_CLIENTE
			AND DESCFIN.E5_LOJA = SD2.D2_LOJA)

WHERE SD2.D_E_L_E_T_ <> '*'

UNION ALL

SELECT
	'01' AS EMPRESA
   ,'SD1' AS ORIGEM
   ,SD1.D1_FILIAL AS FILIAL
   ,SD1.D1_TES AS TES
   ,SD1.D1_FORNECE AS CLIENTE
   ,SD1.D1_LOJA AS LOJA
   ,SD1.D1_GRUPO AS GRUPOPROD
   ,SD1.D1_COD AS PRODUTO
   ,SD1.D1_TP AS TIPOPROD
   ,SD1.D1_TIPO AS TIPONFENTR
   ,' ' AS TIPONFSAID
   ,SD1.D1_QUANT AS QUANTIDADE
   ,CASE SD1.D1_TP
		WHEN 'PA' THEN SD1.D1_QUANT * SB1.B1_LITROS
		WHEN 'PI' THEN SD1.D1_QUANT * SB1.B1_LITROS
		WHEN 'VD' THEN SD1.D1_QUANT * SB1.B1_LITROS
		ELSE 0
	END AS QTLITROS
   ,SD1.D1_DOC AS DOC
   ,SD1.D1_SERIE AS SERIE
   ,SD1.D1_DTDIGIT AS EMISSAO
   ,ISNULL(SE1.E1_VEND1, SF2.F2_VEND1) AS VEND1
   ,ISNULL(SE1.E1_VEND2, SF2.F2_VEND2) AS VEND2
   ,ISNULL(SD2.D2_PEDIDO, '') AS PEDVENDA
   ,ISNULL(SD2.D2_ITEMPV, '') AS ITEMPDVEND
   ,ISNULL(SD2.D2_PVCOND, 0) AS PVCOND
   ,SD1.D1_SEGURO AS SEGURO
   ,SD1.D1_DESPESA AS DESPESA
   ,
	--SD1.D1_VUNIT * SD1.D1_QUANT AS TOTAL,
	SD1.D1_TOTAL AS TOTAL
   ,SD1.D1_VALIPI AS VALIPI
   ,ISNULL(SD2.D2_COMIS1, 0) AS COMIS1
   ,ISNULL(SD2.D2_COMIS2, 0) AS COMIS2
   ,ISNULL(SD2.D2_COMIS3, 0) AS COMIS3
   ,ISNULL(SD2.D2_COMIS4, 0) AS COMIS4
   ,ISNULL(SD2.D2_COMIS5, 0) AS COMIS5
   ,
	--		ISNULL(SD2.D2_VRAPEL*SD1.D1_QUANT/SD2.D2_QUANT, 0) AS RAPELPREV,
	ISNULL(SD2.D2_VRAPEL * SD1.D1_QUANT /
	CASE SD2.D2_QUANT
		WHEN 0 THEN 1
		ELSE SD2.D2_QUANT
	END
	, 0) AS RAPELPREV
   ,CASE SA1.A1_VABARAP
		WHEN '0' THEN 'NAO'
		WHEN '1' THEN 'TOTAL NOTA'
		WHEN '2' THEN 'TOTAL MERCADORIA'
		WHEN '3' THEN 'TOTAL NF - ST'
	END AS BASERAPEL
   ,SD1.D1_VALICM AS VALICM
   ,SD1.D1_VALIMP6 AS VALPIS
   ,SD1.D1_VALIMP5 AS VALCOFINS
   ,SD1.D1_CUSTO AS CUSTOMEDIO
   ,dbo.VA_FQtCx(SD1.D1_COD, SD1.D1_QUANT) AS QTCAIXAS
   ,SF4.F4_DUPLIC AS F4_DUPLIC
   ,SF4.F4_ESTOQUE AS F4_ESTOQUE
   ,SF4.F4_MARGEM AS F4_MARGEM
   ,ISNULL(SD2.D2_EST, '') AS EST
   ,'' AS PRODPAI
   ,SD1.D1_NFORI AS NFORI
   ,SD1.D1_SERIORI AS SERIORI
   ,SD1.D1_ITEMORI AS ITEMORI
   ,SD1.D1_ITEM AS ITEMNOTA
   ,SB1.B1_P_BRT * SD1.D1_QUANT AS PESOBRUTO
   ,SD1.D1_UM AS UMPROD
   ,'' AS UMPRODPAI
   ,SD1.D1_ICMSRET AS ICMSRET
   ,SD1.D1_MOTDEV AS MOTDEV
   ,D1_VALDESC AS DESCONTO
   ,ISNULL(SD2.D2_PRUNIT, 0) AS PRUNIT
   ,D1_CF AS CFOP
   ,F1_TRANSP AS TRANSP
   ,ISNULL(SD2.D2_VACUSTD, 0) AS CUSTOREPOS
   ,20150930 AS DTGERACAO
   ,D1_TOTAL + D1_VALIPI + D1_SEGURO + D1_DESPESA AS VALBRUT
   ,CASE
		WHEN D2_EMISSAO < '20171001' THEN ISNULL((D2_TOTAL + D2_PVCOND - D2_VACUSTD * D2_QUANT - (D2_TOTAL + D2_PVCOND) * D2_COMIS1 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS2 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS3 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS4 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS5 / 100 - ISNULL((SD2.D2_TOTAL + SD2.D2_VALIPI + SD2.D2_ICMSRET) * SF2.F2_VAPERAP / 100, 0) - D2_FRETCIF - D2_VALICM - D2_VALIMP6 - D2_VALIMP5), 0)
		ELSE ISNULL((D2_TOTAL + D2_PVCOND - D2_VACUSTD * D2_QUANT - (D2_TOTAL + D2_PVCOND) * D2_COMIS1 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS2 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS3 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS4 / 100 - (D2_TOTAL + D2_PVCOND) * D2_COMIS5 / 100 - ISNULL((SD2.D2_TOTAL + SD2.D2_VALIPI + SD2.D2_ICMSRET) * SD2.D2_RAPEL / 100, 0) - D2_FRETCIF - D2_VALICM - D2_VALIMP6 - D2_VALIMP5), 0)
	END
	AS MARGEMCONT
   ,

	-- FRETES
	ISNULL(SC5.C5_TPFRETE, ' ') AS TIPOFRETE
   ,

	--ISNULL(CASE SF2.F2_PBRUTO
	--	WHEN 0 THEN 0
	--	ELSE ROUND(SC5.C5_MVFRE * (SB1.B1_P_BRT * SD2.D2_QUANT) / SF2.F2_PBRUTO, 2)
	--END, 0) AS FRETEPREV,
	0 AS FRETEPREV
   ,  -- VAMOS ASSUMIR QUE NAO EXISTE FRETE PREVISTO PARA DEVOLUCAO (NAO VENDEMOS COM INTENCAO DE SER DEVOLVIDO DEPOIS).

	ISNULL((SELECT
			SUM(VALOR_FRETE)
		FROM dbo.VA_FFRETES_ENTRADAS(SD1.D1_FILIAL, SD1.D1_FILIAL, SD1.D1_EMISSAO, SD1.D1_DTDIGIT, SD1.D1_FORNECE, SD1.D1_LOJA, SD1.D1_FORNECE, SD1.D1_LOJA,
		SD1.D1_COD, SD1.D1_COD, SD1.D1_DOC, SD1.D1_SERIE, SD1.D1_DOC, SD1.D1_SERIE, '', '', 'ZZZZZZ', 'ZZ', '', '', 'ZZZZZZZZZ', 'ZZZ'))
	, 0) AS VALORFRETE
   ,0 AS FRETEREENT
   ,0 AS FRETEREDSP
   ,0 AS FRETEPALET
   ,

	-- DESPESAS FINANCEIRAS
	0 AS DFRAPEL
   ,0 AS DFENCART
   ,0 AS DFFEIRAS
   ,0 AS DFFRETES
   ,0 AS DFDESCONT
   ,0 AS DFDEVOL
   ,0 AS DFCAMPANH
   ,0 AS DFABLOJA
   ,0 AS DFCONTRAT
   ,0 AS DFOUTROS
   ,ISNULL(SA1.A1_SATIV1, '') AS ATIVIDADE
   ,0 AS PRAZOMEDIO
   ,SB1.B1_VAMARCM AS MARCA
   ,SB1.B1_GRPEMB AS GRPEMB
   ,SB1.B1_CODLIN AS CODLINHA
   ,ISNULL(SX5SAtiv.X5_DESCRI, '') AS DESCRIATIV
   ,ISNULL(SA1.A1_VACANAL, '') AS CANAL
   ,ISNULL(ZX5Canal.ZX5_18DESC, '') AS DESCRICANAL
   ,0 AS D2_VALFRE
FROM SD1010 AS SD1
INNER JOIN SB1010 AS SB1
	ON (SB1.B1_COD = SD1.D1_COD
			AND SB1.D_E_L_E_T_ <> '*'
			AND SB1.B1_FILIAL = '  ')
LEFT JOIN SF2010 AS SF2
	ON (SF2.F2_DOC = SD1.D1_NFORI
			AND SF2.F2_SERIE = SD1.D1_SERIORI
			AND SF2.D_E_L_E_T_ <> '*'
			AND SF2.F2_FILIAL = SD1.D1_FILIAL)
LEFT JOIN SD2010 AS SD2
	ON (SD2.D2_DOC = SD1.D1_NFORI
			AND SD2.D2_SERIE = SD1.D1_SERIORI
			AND SD2.D2_ITEM = SD1.D1_ITEMORI
			AND SD2.D_E_L_E_T_ <> '*'
			AND SD2.D2_FILIAL = SD1.D1_FILIAL)
LEFT JOIN SE1010 AS SE1   -- ACESSA O FINANCEIRO PARA BUSCAR O VENDEDOR QUE FOI DEBITADA A DEVOLUCAO
	ON (SE1.D_E_L_E_T_ = ''
			AND SE1.E1_FILIAL = SD1.D1_FILIAL
			AND SE1.E1_NUM = SD1.D1_DOC
			AND SE1.E1_PREFIXO = SD1.D1_SERIE
			AND SE1.E1_CLIENTE = SD1.D1_FORNECE
			AND SE1.E1_LOJA = SD1.D1_LOJA)
LEFT JOIN SC5010 AS SC5
	ON (SC5.C5_NUM = SD2.D2_PEDIDO
			AND SC5.D_E_L_E_T_ <> '*'
			AND SC5.C5_FILIAL = SD2.D2_FILIAL)
LEFT JOIN SA1010 AS SA1
	ON (SA1.D_E_L_E_T_ = ''
			AND SA1.A1_FILIAL = '  '
			AND SA1.A1_COD = SD2.D2_CLIENTE
			AND SA1.A1_LOJA = SD2.D2_LOJA)
LEFT JOIN SX5010 AS SX5SAtiv
	ON (SX5SAtiv.D_E_L_E_T_ = ''
			AND SX5SAtiv.X5_FILIAL = '  '
			AND SX5SAtiv.X5_TABELA = 'T3'
			AND SX5SAtiv.X5_CHAVE = SA1.A1_SATIV1)
LEFT JOIN ZX5010 AS ZX5Canal
	ON (ZX5Canal.D_E_L_E_T_ = ''
			AND ZX5Canal.ZX5_FILIAL = '  '
			AND ZX5Canal.ZX5_TABELA = '18'
			AND ZX5Canal.ZX5_18COD = SA1.A1_VACANAL)
INNER JOIN SF4010 AS SF4
	ON (SD1.D1_TES = SF4.F4_CODIGO
			AND SF4.D_E_L_E_T_ <> '*'
			AND SF4.F4_MARGEM = '2'
			AND SF4.F4_FILIAL = '  ')
INNER JOIN SF1010 AS SF1
	ON (SD1.D1_DOC = SF1.F1_DOC
			AND SD1.D1_SERIE = SF1.F1_SERIE
			AND SD1.D1_FORNECE = SF1.F1_FORNECE
			AND SD1.D1_LOJA = SF1.F1_LOJA
			AND SF1.D_E_L_E_T_ <> '*'
			AND SD1.D1_FILIAL = SF1.F1_FILIAL)
WHERE SD1.D_E_L_E_T_ <> '*'
--	AND SD1.D1_TIPO = 'D'

GO


