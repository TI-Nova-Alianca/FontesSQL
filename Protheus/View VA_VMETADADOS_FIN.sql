SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER VIEW [dbo].[VA_VMETADADOS_FIN]
AS

-- Cooperativa Vinicola Nova Alianca Ltda
-- View para buscar pendencias de integracao Metadados X financeiro do Protheus.
-- Autor: Robert Koch
-- Data:  24/05/2016
-- Historico de alteracoes:
--

SELECT
		-- Tipo de item 22:remessa para banco: Busca filial a partir de tabela de detalhamento da remessa.
		SUBSTRING(CASE
			WHEN C.TIPOITEMCONTAPAGAR = '22' THEN D.ESTABELECIMENTO
			ELSE C.ESTABELECIMENTO
		END, 3, 2) AS FILIAL
		,C.NROSEQUENCIAL
		,C.NROREMESSA
		,dbo.VA_DatetimeToVarchar(C.DATAINCLUSAO) AS EMISSAO
		,dbo.VA_DatetimeToVarchar(C.DATAVENCIMENTO) AS VENCTO
		,CASE
			WHEN C.TIPOITEMCONTAPAGAR = '22' THEN D.VALOR
			ELSE C.VALOR
		END AS VALOR
		,SUBSTRING(C.DESCRICAO20, 1, 6) AS FORNECE
		,RTRIM(C.DESCRICAO250) + CASE
			WHEN C.TIPOITEMCONTAPAGAR = '22' THEN ' (Rem.' + CAST(C.NROREMESSA AS VARCHAR) + ')'
			ELSE ''
		END AS HIST
		,RTRIM(C.NUMERODOCUMENTO) AS NATUREZA
	FROM SIRH.dbo.RHCONTASPAGARHIST C
	LEFT JOIN (SELECT
			EMPRESA
			,BANCO
			,NROREMESSA
			,ESTABELECIMENTO
			,SUM(VALOR) AS VALOR
		FROM SIRH.dbo.RHDEPOSITOSANALITICO D
		WHERE EMPRESA = '00' + '01'
		GROUP BY	EMPRESA
					,BANCO
					,NROREMESSA
					,ESTABELECIMENTO) AS D
		ON (D.EMPRESA = C.EMPRESA
		AND D.BANCO = C.BANCO
		AND D.NROREMESSA = C.NROREMESSA
		AND C.TIPOITEMCONTAPAGAR = '22')
	WHERE C.EMPRESA = '00' + '01'
	AND C.STATUSREGISTRO = '02'
	AND C.TIPOITEMCONTAPAGAR != '04'
	AND C.TIPOITEMCONTAPAGAR != '05'
	AND C.TIPOITEMCONTAPAGAR != '10'
GO
